message(STATUS "Delivery root: ${DELIVERY_ROOT}")
message(STATUS "AIR cli path: ${AIR_CLI}")
message(STATUS "Delivery version: ${DELIVERY_VERSION}")
message(STATUS "Toolchain ID: ${DELIVERY_TOOLCHAIN_ID}")
message(STATUS "Delivery file: ${DELIVERY_FILE}")
set(DELIVERY_ID "${DELIVERY_VERSION}_${DELIVERY_TOOLCHAIN_ID}")
if(EXISTS ${DELIVERY_ROOT}/.delivery_id)
    file(READ ${DELIVERY_ROOT}/.delivery_id CURRENT_DELIVERY_ID)
endif()

message(STATUS "Current delivery ID: ${CURRENT_DELIVERY_ID}")
message(STATUS "Delivery ID: ${DELIVERY_ID}")
if (NOT "${CURRENT_DELIVERY_ID}" STREQUAL "${DELIVERY_ID}")
    execute_process(COMMAND ${CMAKE_COMMAND} -E make_directory "${DELIVERY_ROOT}")
    configure_file("${DELIVERY_FILE}" "${DELIVERY_ROOT}/${DELIVERY_ID}")
    file(STRINGS ${DELIVERY_ROOT}/${DELIVERY_ID} path_list)
    foreach(path ${path_list})
        get_filename_component(name "${path}" NAME)
        message(STATUS "Downloading: ${path} to ${DELIVERY_ROOT}/${name}")
        execute_process(COMMAND "${AIR_CLI}" cp "${path}" "${DELIVERY_ROOT}/${name}" RESULT_VARIABLE result)
        if(NOT result EQUAL "0")
            message(FATAL_ERROR "Failed to download: ${path}  to ${DELIVERY_ROOT}/${name}")
        endif()
        message(STATUS "Extracting : ${DELIVERY_ROOT}/${name}")
        execute_process(COMMAND "${CMAKE_COMMAND}" -E tar xf ${name} WORKING_DIRECTORY ${DELIVERY_ROOT} RESULT_VARIABLE result)
        if(NOT result EQUAL "0")
            message(FATAL_ERROR "Failed to extract ${name}")
        endif()
    endforeach()
    file(WRITE ${DELIVERY_ROOT}/.delivery_id ${DELIVERY_ID})
endif()